(ns eca.features.chat.tool-calls
  (:require
   [clojure.string :as string]
   [eca.db :as db]
   [eca.features.chat.lifecycle :as lifecycle]
   [eca.features.chat.parent-coordinator :as parent-coordinator]
   [eca.features.hooks :as f.hooks]
   [eca.features.tools :as f.tools]
   [eca.features.tools.agent :as f.tools.agent]
   [eca.features.tools.mcp :as f.tools.mcp]
   [eca.features.tools.util :as tools.util]
   [eca.llm-util :as llm-util]
   [eca.logger :as logger]
   [eca.shared :as shared :refer [assoc-some]]))

(set! *warn-on-reflection* true)

(defn ^:private check-subagent-max-steps!
  "Check if subagent has reached max steps. Increments step count.
   Returns true if max steps reached, false otherwise.
   When max-steps is nil, the subagent runs with no step limit.
   Only applies to subagents (chats with :subagent)."
  [db* chat-id]
  (when-let [subagent (get-in @db* [:chats chat-id :subagent])]
    (let [max-steps (:max-steps subagent)
          new-db (swap! db* update-in [:chats chat-id :current-step] (fnil inc 0))
          new-step (get-in new-db [:chats chat-id :current-step])]
      (when max-steps
        (>= new-step max-steps)))))

(def ^:private logger-tag "[CHAT]")

(def ^:private rejected-tool-call-message
  "Leading message for every rejected/blocked tool call result sent back to the
   LLM. Some models otherwise assume a rejected edit was actually applied (#507),
   so this states plainly that the call never ran and changed nothing."
  (str "This tool call did NOT run and made NO changes. Any files, code, or "
       "system state remain exactly as they were before this call, so do not "
       "assume the action was applied or succeeded. If it is still needed, ask "
       "the user how to proceed or try a different approach."))

(defn ^:private rejected-tool-call-output-contents
  "LLM-facing :contents for a rejected/blocked tool call: the standard
   `rejected-tool-call-message` plus the specific reason, when present."
  [reason-text]
  [{:type :text
    :text (cond-> rejected-tool-call-message
            (shared/not-blank reason-text) (str "\nReason: " reason-text))}])

(defn ^:private update-tool-output-contents!
  "Apply `f` to the :contents of the tool_call_output message matching
   `tool-call-id`, a no-op when no such message exists. Scans messages backwards
   since the tool output is usually one of the last items."
  [db* chat-id tool-call-id f]
  (swap! db* update-in [:chats chat-id :messages]
         (fn [messages]
           (let [idx (llm-util/find-last-msg-idx
                      #(and (= "tool_call_output" (:role %))
                            (= tool-call-id (get-in % [:content :id])))
                      messages)]
             (if idx
               (update-in messages [idx :content :output :contents] f)
               messages)))))

(defn ^:private append-post-tool-additional-context!
  "Append additionalContext (wrapped as XML) from a postToolCall hook to the
   matching tool_call_output message so LLM sees it in the next round."
  [db* chat-id tool-call-id additional-context]
  (when-not (string/blank? additional-context)
    (let [entry {:type :text :text (lifecycle/wrap-additional-context additional-context)}]
      (update-tool-output-contents! db* chat-id tool-call-id #(conj % entry)))))

(defn ^:private replace-tool-output!
  "Replace the tool result content of the matching tool_call_output message.
   replacedOutput is a string that replaces the original tool result.
   An empty string hides the result (empty content)."
  [db* chat-id tool-call-id replaced-output]
  (update-tool-output-contents! db* chat-id tool-call-id
                                (constantly [{:type :text :text replaced-output}])))

(defn ^:private post-tool-call-stop-info
  "Single pass over tool-call states. Returns
   {:stop-turn? boolean :stop-reason string-or-nil :stop-hook-name string-or-nil}
   describing whether any postToolCall hook requested a current-turn stop
   (continue:false) and the first stop reason / hook name found, if any."
  [tool-calls prompt-id]
  (let [current-prompt-stop? (fn [state]
                               (and (some? prompt-id)
                                    (:post-tool-call-stop? state)
                                    (= prompt-id (:post-tool-call-stop-prompt-id state))))]
    (reduce (fn [acc [_ state]]
              (if (current-prompt-stop? state)
                (cond-> (assoc acc :stop-turn? true)
                  (and (nil? (:stop-reason acc)) (:post-tool-call-stop-reason state))
                  (assoc :stop-reason (:post-tool-call-stop-reason state))

                  (and (nil? (:stop-hook-name acc)) (:post-tool-call-stop-hook-name state))
                  (assoc :stop-hook-name (:post-tool-call-stop-hook-name state)))
                acc))
            {:stop-turn? false :stop-reason nil :stop-hook-name nil}
            tool-calls)))

;;; Helper functions for tool call state management

(defn get-tool-call-state
  "Get the complete state map for a specific tool call."
  [db chat-id tool-call-id]
  (get-in db [:chats chat-id :tool-calls tool-call-id]))

(defn get-active-tool-calls
  "Returns a map of tool-call-id -> tool calls that are still active.

  Active tool calls are those not in the following states: :completed, :rejected."
  [db chat-id]
  (->> (get-in db [:chats chat-id :tool-calls] {})
       (remove (fn [[_ state]]
                 (#{:completed :rejected} (:status state))))
       (into {})))

(defn ^:private run-post-tool-call-hooks!
  "Run postToolCall hooks and append any additionalContext to the tool output.
   Returns {:stop-turn? boolean :stop-reason string-or-nil} when a hook returns
   continue:false."
  [db* chat-ctx tool-call-id event-data]
  (let [chat-id (:chat-id chat-ctx)
        db @db*
        tool-call-state (get-tool-call-state db chat-id tool-call-id)
        native-tools (filter #(= :native (:origin %))
                             (f.tools/all-tools chat-id (:agent chat-ctx) db (:config chat-ctx)))
        hook-state* (atom {:stop-turn? false :stop-reason nil :stop-hook-name nil})]
    (f.hooks/trigger-if-matches!
     :postToolCall
     (merge (f.hooks/chat-hook-data db chat-ctx)
            {:tool-name (:name tool-call-state)
             :server (:server tool-call-state)
             :tool-input (:arguments tool-call-state)
             :tool-response (tools.util/contents->text (:outputs event-data))
             :error (:error event-data)
             :tool-call-id tool-call-id})
     {:on-before-action (partial lifecycle/notify-before-hook-action! chat-ctx)
      :on-after-action (fn [{:keys [parsed raw-error name exit] :as result}]
                         (let [stop-reason (shared/not-blank (get parsed "stopReason"))]
                           ;; Always notify UI, tagged with the tool-call-id for correlation.
                           (lifecycle/notify-after-hook-action!
                            chat-ctx (assoc result :tool-call-id tool-call-id))
                           (cond
                             ;; postToolCall exit 2 hides the original result from the LLM.
                             ;; stderr becomes the replacement content; empty stderr falls back to a
                             ;; default placeholder so the LLM still sees a meaningful signal.
                             ;; raw-error is already normalized to nil-or-non-empty-string by
                             ;; run-and-parse-output!, so a plain `or` is enough here.
                             (= f.hooks/hook-rejection-exit-code exit)
                             (replace-tool-output!
                              (:db* chat-ctx)
                              (:chat-id chat-ctx)
                              tool-call-id
                              (or raw-error "[Tool output hidden by hook]"))

                             (zero? exit)
                             (do
                               (when (false? (get parsed "continue" true))
                                 (swap! hook-state*
                                        #(-> (assoc % :stop-turn? true :stop-hook-name name)
                                             (assoc-some :stop-reason stop-reason))))
                               (when-let [replaced-output (get parsed "replacedOutput")]
                                 (if (string? replaced-output)
                                   (replace-tool-output!
                                    (:db* chat-ctx)
                                    (:chat-id chat-ctx)
                                    tool-call-id
                                    replaced-output)
                                   (logger/warn logger-tag "Ignoring non-string replacedOutput from postToolCall hook"
                                                {:hook-name    name
                                                 :tool-call-id tool-call-id
                                                 :value        replaced-output})))
                               ;; If hook provided additionalContext, append as XML to the tool output
                               (when-let [ac (shared/not-blank (get parsed "additionalContext"))]
                                 (append-post-tool-additional-context!
                                  (:db* chat-ctx)
                                  (:chat-id chat-ctx)
                                  tool-call-id
                                  ac))))))
      :native-tools native-tools}
     db
     (:config chat-ctx))
    @hook-state*))

;;; Event-driven state machine for tool calls

(def ^:private tool-call-state-machine
  "State machine for tool call lifecycle management.

   Maps [current-status event] -> {:status new-status :actions [action-list]}

   Statuses:
   - :initial             - The initial status.  Ephemeral.
   - :preparing           - Preparing the arguments for the tool call.
   - :check-approval      - Checking to see if the tool call is approved, via config or asking the user.
   - :waiting-approval    - Waiting for user approval or rejection.
   - :execution-approved  - The tool call has been approved for execution, via config or asking the user.
   - :executing           - The tool call is executing.
   - :rejected            - Rejected before starting execution.  Terminal status.
   - :cleanup             - Cleaning up the state after finishing execution.  Either after normal execution or after being user stopped.
   - :completed           - Tool call completion.  Perhaps with tool errors.  With or without being interrupted. Terminal status.
   - :stopping            - In the process of stopping, after execution has started, but before it completed. After getting a :stop-request.

   Events:
   - :tool-prepare        - LLM preparing tool call (can happen multiple times).
   - :tool-run            - LLM ready to run tool call.
   - :user-approve        - User approves tool call.
   - :user-reject         - User rejects tool call.
   - :send-reject         - A made-up event to cause a toolCallReject.  Used in a context where the message data is available.
   - :execution-start     - Tool call execution begins.
   - :execution-end       - Tool call completes normally.  Perhaps with its own errors.
   - :cleanup-finished    - Cleaned up the state after tool call completes, either normally or interrupted.
   - :stop-requested      - An event to request that active tool calls be stopped.
   - :resources-created   - Some new resources were created during the call.
   - :resources-destroyed - Some existing resources were destroyed.
   - :stop-attempted      - We have done all we can to stop the tool call.  The tool may or may not be actually stopped.

   Actions:
   - send-* notifications
   - set-* set various state values
   - add- and remove-resources
   - init, delivery and removal of approval and future-cleanup promises
   - future cancellation
   - logging/metrics

   Note: All actions are run in the order specified.
   Note: The :send-* actions should be last, so that they have the latest values of the state context.
   Note: The :status is updated before any actions are run, so the actions are in the context of the latest :status.

   Note: all choices (i.e. conditionals) have to be made in code and result
   in different events being sent to the state machine.
   For example, from the :check-approval state you can either get
   a :approval-ask event, a :approval-allow event, or a :approval-deny event."
  {;; Note: transition-tool-call! treats no existing state as :initial state
   [:initial :tool-prepare]
   {:status :preparing
    :actions [:init-tool-call-state :send-toolCallPrepare]}

   [:preparing :tool-prepare]
   {:status :preparing
    :actions [:send-toolCallPrepare]} ; Multiple prepares allowed

   [:preparing :tool-run]
   {:status :check-approval
    :actions [:init-arguments :init-approval-promise :init-future-cleanup-promise :send-toolCallRun]}
   ;; All promises must be deref'ed.

   [:check-approval :approval-ask]
   {:status :waiting-approval
    :actions [:send-progress]}

   [:check-approval :approval-allow]
   {:status :execution-approved
    :actions [:set-decision-reason :deliver-approval-true]}

   [:check-approval :approval-deny]
   {:status :rejected
    :actions [:set-decision-reason :deliver-approval-false]}

   [:waiting-approval :user-approve]
   {:status :execution-approved
    :actions [:set-decision-reason :deliver-approval-true]}

   [:waiting-approval :user-reject]
   {:status :rejected
    :actions [:set-decision-reason :deliver-approval-false :log-rejection]}

   [:rejected :send-reject]
   {:status :rejected
    :actions [:send-toolCallRejected]}

   [:execution-approved :execution-start]
   {:status :executing
    :actions [:set-start-time :add-future :send-toolCallRunning :send-progress]}

   [:executing :execution-end]
   {:status :cleanup
    :actions [:save-execution-result :deliver-future-cleanup-completed :send-toolCalled :log-metrics :send-progress :trigger-post-tool-call-hook]}

   [:cleanup :cleanup-finished]
   {:status :completed
    :actions [:destroy-all-resources :remove-all-resources :remove-all-promises :remove-future]}

   [:executing :resources-created]
   {:status :executing
    :actions [:add-resources]}

   [:executing :resources-destroyed]
   {:status :executing
    :actions [:remove-resources]}

   [:stopping :resources-destroyed]
   {:status :stopping
    :actions [:remove-resources]}

   [:stopping :stop-attempted]
   {:status :cleanup
    :actions [:save-execution-result :deliver-future-cleanup-completed :send-toolCallRejected :trigger-post-tool-call-hook]}

   ;; And now all the :stop-requested transitions

   ;; Note: There are, currently, no transitions from the terminal statuses
   ;; on :stop-requested.
   ;; This is because :stop-requested is only sent to active statuses.
   ;; Also, we don't want to have transitions out from terminal states,
   ;; even if they are self-transitions.

   [:executing :stop-requested]
   {:status :stopping
    :actions [:cancel-future]}

   ;; ignore :stop-requested
   [:cleanup :stop-requested]
   {:status :cleanup
    :actions []}

   ;; ignore :stop-requested
   [:stopping :stop-requested]
   {:status :stopping
    :actions []}

   [:execution-approved :stop-requested]
   {:status :cleanup
    :actions [:send-toolCallRejected]}

   [:waiting-approval :stop-requested]
   {:status :rejected
    :actions [:set-decision-reason :deliver-approval-false]}

   [:check-approval :stop-requested]
   {:status :rejected
    :actions [:set-decision-reason :deliver-approval-false]}

   [:preparing :stop-requested]
   {:status :cleanup
    :actions [:set-decision-reason :send-toolCallRejected]}

   [:initial :stop-requested] ; Nothing sent yet, just mark as stopped
   {:status :cleanup
    :actions []}})

(defn ^:private execute-action!
  "Execute a single action during state transition"
  [action db* chat-ctx tool-call-id event-data]
  (case action
    ;; Notification actions
    :save-execution-result
    (swap! db* update-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id]
           merge
           (select-keys event-data [:outputs :error :total-time-ms]))

    :send-progress
    (lifecycle/send-content! chat-ctx :system
                             {:type :progress
                              :state :running
                              :text (:progress-text event-data)})

    :send-toolCallPrepare
    (lifecycle/send-content! chat-ctx :assistant
                             (assoc-some
                              {:type :toolCallPrepare
                               :id tool-call-id
                               :name (:name event-data)
                               :server (:server event-data)
                               :origin (:origin event-data)
                               :arguments-text (:arguments-text event-data)}
                              :summary (:summary event-data)))

    :send-toolCallRun
    (lifecycle/send-content! chat-ctx :assistant
                             (assoc-some
                              {:type :toolCallRun
                               :id tool-call-id
                               :name (:name event-data)
                               :server (:server event-data)
                               :origin (:origin event-data)
                               :arguments (:arguments event-data)
                               :manual-approval (:manual-approval event-data)}
                              :details (:details event-data)
                              :summary (:summary event-data)))

    :send-toolCallRunning
    (lifecycle/send-content! chat-ctx :assistant
                             (assoc-some
                              {:type :toolCallRunning
                               :id tool-call-id
                               :name (:name event-data)
                               :server (:server event-data)
                               :origin (:origin event-data)
                               :arguments (:arguments event-data)}
                              :details (:details event-data)
                              :summary (:summary event-data)))

    :send-toolCalled
    ;; Tool results may include image content blocks (e.g. from MCP image
    ;; tools). The protocol declares toolCalled :outputs as text-only, so we
    ;; partition: text outputs go into the toolCalled event, images are
    ;; emitted as separate ChatImageContent events tagged :assistant. This
    ;; reuses the same wire shape produced by the OpenAI Responses-API
    ;; built-in image_generation tool (see chat.clj on-message-received
    ;; :image branch).
    ;; Only partition when outputs is a sequence containing image content
    ;; maps; otherwise pass it through unchanged. This preserves
    ;; pre-existing behaviors where :outputs can be nil or a plain string
    ;; (used by some non-MCP code paths and tests).
    (let [outputs (:outputs event-data)
          image? #(and (map? %) (= :image (:type %)))
          image-outputs (when (sequential? outputs) (filter image? outputs))
          text-outputs (if (seq image-outputs)
                         (vec (remove image? outputs))
                         outputs)]
      (lifecycle/send-content! chat-ctx :assistant
                               (assoc-some
                                {:type :toolCalled
                                 :id tool-call-id
                                 :origin (:origin event-data)
                                 :name (:name event-data)
                                 :server (:server event-data)
                                 :arguments (:arguments event-data)
                                 :error (:error event-data)
                                 :total-time-ms (:total-time-ms event-data)
                                 :outputs text-outputs}
                                :details (:details event-data)
                                :summary (:summary event-data)))
      (doseq [img image-outputs]
        (lifecycle/send-content! chat-ctx :assistant
                                 {:type :image
                                  :media-type (:media-type img)
                                  :base64 (:base64 img)})))

    :send-toolCallRejected
    (let [tool-call-state (get-tool-call-state @db* (:chat-id chat-ctx) tool-call-id)
          name (:name tool-call-state)
          server (:server tool-call-state)
          origin (:origin tool-call-state)
          arguments (:arguments tool-call-state)]
      (lifecycle/send-content! chat-ctx :assistant
                               (assoc-some
                                {:type :toolCallRejected
                                 :id tool-call-id
                                 :origin (or (:origin event-data) origin)
                                 :name (or (:name event-data) name)
                                 :server (or (:server event-data) server)
                                 :arguments (or (:arguments event-data) arguments)
                                 :reason (:code (:reason event-data) :user)}
                                :details (:details event-data)
                                :summary (:summary event-data))))

    :trigger-post-tool-call-hook
    (let [{:keys [stop-turn? stop-reason stop-hook-name]} (run-post-tool-call-hooks! db* chat-ctx tool-call-id event-data)]
      (when (or stop-turn? stop-reason)
        (swap! db* update-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id]
               (fn [state]
                 (cond-> (or state {})
                   stop-turn? (assoc :post-tool-call-stop? true
                                     :post-tool-call-stop-prompt-id (:prompt-id chat-ctx))
                   stop-reason (assoc :post-tool-call-stop-reason stop-reason)
                   stop-hook-name (assoc :post-tool-call-stop-hook-name stop-hook-name))))))

    ;; Actions on parts of the state
    :deliver-approval-false
    (deliver (get-in @db* [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :approved?*])
             false)

    :deliver-approval-true
    (deliver (get-in @db* [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :approved?*])
             true)

    :deliver-future-cleanup-completed
    (when-let [p (get-in @db* [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :future-cleanup-complete?*])]
      (deliver p true))

    :cancel-future
    (when-let [f (get-in @db* [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :future])]
      (future-cancel f))

    :destroy-all-resources
    (when-let [resources (get-in @db* [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :resources])]
      (when-not (empty? resources)
        (doseq [[resource-kwd resource] resources]
          (f.tools/tool-call-destroy-resource! (:full-name event-data) resource-kwd resource))))

    ;; State management actions
    :init-tool-call-state
    (swap! db* update-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id] assoc
           ;; :status (keyword) is initialized by the state transition machinery
           ;; :approved?* (promise) is initialized by the :init-approval-promise action
           ;; :future-cleanup-complete?* (promise) is initialized by the :init-future-cleanup-promise action
           ;; :arguments (map), :summary, :details and :manual-approval are
           ;; initialized by the :init-arguments action
           ;; :start-time (long) is initialized by the :set-start-time action
           ;; :future (future) is added by the :add-future action and removed by :remove-future
           ;; :resources (map) is updated by the :add-resources and :remove-resources actions,
           ;; and removed wholesale by :remove-all-resources during cleanup
           :name (:name event-data)
           :full-name (:full-name event-data)
           :server (:server event-data)
           :arguments (:arguments event-data)
           :origin (:origin event-data)
           :decision-reason {:code :none
                             :text "No reason"})

    :init-approval-promise
    (swap! db* assoc-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :approved?*]
           (:approved?* event-data))

    :init-future-cleanup-promise
    (swap! db* assoc-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :future-cleanup-complete?*]
           (:future-cleanup-complete?* event-data))

    :init-arguments
    (swap! db* update-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id]
           #(-> %
                (assoc :arguments (:arguments event-data))
                (assoc-some :summary (:summary event-data)
                            :details (:details event-data)
                            :manual-approval (:manual-approval event-data))))

    :set-decision-reason
    (swap! db* assoc-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :decision-reason]
           (:reason event-data))

    :set-start-time
    (swap! db* assoc-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :start-time]
           (:start-time event-data))

    :add-future
    (swap! db* assoc-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :future]
           ;; start the future by forcing the delay and save it in the call state
           (force (:delayed-future event-data)))

    :remove-future
    (swap! db* update-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id]
           dissoc :future)

    :add-resources
    (swap! db* update-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :resources]
           merge (:resources event-data))

    :remove-resources
    (swap! db* update-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :resources]
           #(apply dissoc %1 %2) (:resources event-data))

    :remove-all-resources
    (swap! db* update-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id]
           dissoc :resources)

    :remove-all-promises
    (swap! db* update-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id]
           dissoc :approved?* :future-cleanup-complete?*)

    ;; Logging actions
    :log-rejection
    (logger/info logger-tag "Tool call rejected"
                 {:tool-call-id tool-call-id :reason (:reason event-data)})

    :log-metrics
    (logger/debug logger-tag "Tool call completed"
                  {:tool-call-id tool-call-id :duration (:duration event-data)})

    ;; Default case for unknown actions
    (logger/warn logger-tag "Unknown action" {:action action :tool-call-id tool-call-id})))

(defn transition-tool-call!
  "Execute an event-driven state transition for a tool call.

   Args:
   - db*: Database atom
   - chat-ctx: Chat context map with :chat-id, :request-id, :messenger
   - tool-call-id: Tool call identifier
   - event: Event keyword (e.g., :tool-prepare, :tool-run, :user-approve)
   - event-data: Optional map with event-specific data

   Returns: {:status new-status :actions actions-executed}

   Throws: ex-info if the transition is invalid for the current state.

   Note: The status is updated before any actions are run.
   Actions are run in the order specified."
  [db* chat-ctx tool-call-id event & [event-data]]
  (let [current-state (get-tool-call-state @db* (:chat-id chat-ctx) tool-call-id)
        current-status (:status current-state :initial) ; Default to :initial if no state
        transition-key [current-status event]
        {:keys [status actions]} (get tool-call-state-machine transition-key)]

    (logger/debug logger-tag "Tool call transition"
                  {:tool-call-id tool-call-id :current-status current-status :event event :status status})

    (when-not status
      (let [valid-events (map second (filter #(= current-status (first %))
                                             (keys tool-call-state-machine)))]
        (throw (ex-info "Invalid state transition"
                        {:current-status current-status
                         :event event
                         :tool-call-id tool-call-id
                         :valid-events valid-events}))))

    ;; Atomic status update
    (swap! db* assoc-in [:chats (:chat-id chat-ctx) :tool-calls tool-call-id :status] status)

    ;; Execute all actions sequentially
    (doseq [action actions]
      (execute-action! action db* chat-ctx tool-call-id event-data))

    (lifecycle/trigger-chat-status-hook! (assoc chat-ctx :db* db*))

    {:status status :actions actions}))

(def ^:private hook-approval-rank
  {"allow" 1
   "ask" 2
   "deny" 3})

(defn ^:private stronger-hook-approval
  [current candidate]
  (if (> (get hook-approval-rank candidate 0)
         (get hook-approval-rank current 0))
    candidate
    current))

(defn ^:private process-pre-tool-call-hook-result
  "Pure function: fold a single hook result into accumulated state.

   acc is {:hook-results [], :approval-override nil,
           :tool-call-rejected-by-hook? false, :tool-call-rejection-reason nil,
           :stop-turn? false, :stop-reason nil, :stop-hook-name nil}"
  [acc result]
  (let [parsed (:parsed result)
        success? (zero? (:exit result))
        exit-code-2? (= f.hooks/hook-rejection-exit-code (:exit result))
        stop-turn-result? (and success? (false? (get parsed "continue" true)))
        ;; success? (exit 0) and exit-code-2? are mutually exclusive, so the
        ;; approval field is only read on exit 0.
        hook-approval (when success?
                        (get parsed "approval"))]
    (cond-> (update acc :hook-results conj result)
      ;; Handle current-turn stop for successful hooks, even without rejection.
      stop-turn-result?
      (merge {:stop-turn? true
              :stop-reason (shared/not-blank (get parsed "stopReason"))
              :stop-hook-name (:name result)})

      ;; Handle rejection (exit code 2 or successful explicit deny)
      (or exit-code-2? (= "deny" hook-approval))
      (merge {:tool-call-rejected-by-hook? true
              :tool-call-rejection-reason (if exit-code-2?
                                            (or (:raw-error result)
                                                "Tool call rejected by hook")
                                            (or (shared/not-blank (get parsed "additionalContext"))
                                                "Tool call rejected by hook"))})

      ;; Handle approval override with hook precedence: deny > ask > allow.
      (contains? hook-approval-rank hook-approval)
      (update :approval-override stronger-hook-approval hook-approval))))

(defn ^:private decide-tool-call-action
  "Decides what action to take for a tool call, running hooks and collecting their results.

   Returns a plan (data) with:
   - :decision (:ask | :allow | :deny)
   - :arguments (potentially modified by hooks)
   - :approval-override (from hooks)
   - :tool-call-rejected-by-hook? (boolean, explicit hook rejection via exit 2 or approval:deny)
   - :tool-call-blocked-by-hook? (boolean, hook rejection or current-turn stop prevents execution)
   - :reason (map with :code and :text, when decision is :allow or :deny)
   - :stop-turn? (boolean, when a successful hook returns continue:false)
   - :stop-reason (string, for hook turn stops)
   - :stop-hook-name (string, for hook turn stops)

   The on-before-hook-action and on-after-hook-action callbacks are optional (default to noops)
   and are used for UI notifications. In tests, these can be omitted."
  [{:keys [full-name arguments] tool-call-id :id} all-tools db config agent-name chat-id
   & [{:keys [on-before-hook-action on-after-hook-action trust full-model variant]
       :or {on-before-hook-action (fn [_] nil)
            on-after-hook-action (fn [_] nil)}}]]
  (let [{:keys [name server] :as tool} (f.tools/resolve-tool full-name all-tools)
        server-name (:name server)
        native-tools (filter #(= :native (:origin %)) all-tools)

        ;; 1. Determine approval (trust promotion handled inside f.tools/approval)
        approval (f.tools/approval all-tools tool arguments db config agent-name {:trust trust})
        trusted? (= :trust/allow approval)
        effective-approval (if trusted? :allow approval)

        ;; ask_user always blocks waiting for the user regardless of trust/auto-allow.
        ;; Surface that as :ask to preToolCall hooks so notification hooks
        ;; (e.g. matching .approval == "ask") also fire when the chat is blocked on
        ;; a user question. Only override when allowed; explicit deny is preserved.
        hook-approval (if (and (= "eca" server-name)
                               (= "ask_user" name)
                               (= :allow effective-approval))
                        :ask
                        effective-approval)

        ;; 2. Run hooks to collect modifications and approval overrides
        hook-state* (atom {:hook-results []
                           :approval-override nil
                           :tool-call-rejected-by-hook? false
                           :tool-call-rejection-reason nil
                           :stop-turn? false
                           :stop-reason nil
                           :stop-hook-name nil})

        _ (f.hooks/trigger-if-matches!
           :preToolCall
           (merge (f.hooks/chat-hook-data db {:chat-id chat-id
                                              :agent agent-name
                                              :full-model full-model
                                              :variant variant})
                  {:tool-name name
                   :server server-name
                   :tool-input arguments
                   :approval hook-approval
                   :tool-call-id tool-call-id})
           {:on-before-action on-before-hook-action
            :on-after-action (fn [result]
                               (on-after-hook-action (assoc result :tool-call-id tool-call-id))
                               (swap! hook-state* process-pre-tool-call-hook-result result))
            :native-tools native-tools}
           db
           config)

        ;; 3. Merge all updatedInput from hooks
        {:keys [hook-results approval-override tool-call-rejected-by-hook?
                tool-call-rejection-reason stop-turn? stop-reason stop-hook-name]} @hook-state*
        updated-inputs (keep (fn [{:keys [parsed exit]}]
                               (when (zero? exit)
                                 (get parsed "updatedInput")))
                             hook-results)
        final-arguments (if (not-empty updated-inputs)
                          (reduce merge arguments updated-inputs)
                          arguments)
        arguments-modified? (boolean (seq updated-inputs))

        ;; A current-turn stop is not a tool-specific rejection, but it still
        ;; blocks this prepared tool and downstream must stop the agent loop.
        ;; tool-call-blocked-by-hook? = any hook intervention that prevents the
        ;; tool from running.
        tool-call-blocked-by-hook? (or tool-call-rejected-by-hook? stop-turn?)
        turn-stopped-only? (and stop-turn? (not tool-call-rejected-by-hook?))

        ;; 4. Determine final approval. Hook deny/stop wins, but hook allow
        ;; cannot override config deny/ask; it only confirms an already-allowed call.
        final-decision (cond
                         tool-call-blocked-by-hook? :deny
                         (= :deny effective-approval) :deny
                         (= :ask effective-approval) :ask
                         (= "ask" approval-override) :ask
                         :else effective-approval)

        ;; 5. Build the reason map. A hook current-turn stop is represented
        ;; separately from a tool-specific rejection so downstream code and the
        ;; LLM-visible history do not conflate the two intents.
        reason (case final-decision
                 :allow (if trusted?
                          {:code :trust-allow
                           :text "Tool call allowed by trust mode"}
                          {:code :user-config-allow
                           :text "Tool call allowed by user config"})
                 :deny (cond
                         ;; Turn stop: LLM-visible text must stay neutral;
                         ;; stopReason is surfaced separately to the user.
                         turn-stopped-only?
                         {:code :hook-stopped
                          :text "Tool call stopped by hook"}

                         tool-call-rejected-by-hook?
                         {:code :hook-rejected
                          :text tool-call-rejection-reason}

                         :else
                         {:code :user-config-deny
                          :text "Tool call rejected by user config"})
                 nil)]

    ;; Return the decision plan
    (cond-> {:decision final-decision
             :arguments final-arguments
             :approval-override approval-override
             :tool-call-rejected-by-hook? tool-call-rejected-by-hook?
             :tool-call-blocked-by-hook? tool-call-blocked-by-hook?
             :stop-turn? stop-turn?
             :arguments-modified? arguments-modified?}
      reason (assoc :reason reason)
      stop-turn? (assoc :stop-reason stop-reason
                        :stop-hook-name stop-hook-name))))

(defn ^:private spawn-agent-tool-call?
  [all-tools {:keys [full-name]}]
  (let [{:keys [origin name server]} (f.tools/resolve-tool full-name all-tools)]
    (and (= :native origin)
         (= "eca" (:name server))
         (= "spawn_agent" name))))

(defn on-tools-called! [{:keys [db* config chat-id agent messenger metrics] :as chat-ctx}
                        received-msgs* add-to-history! user-messages]
  (fn [tool-calls]
    ;; postToolCall hooks report continue:false through the tool call state,
    ;; accumulated per-tool by the state machine action :trigger-post-tool-call-hook.
    (let [all-tools (f.tools/all-tools chat-id agent @db* config)
          max-steps-reached? (check-subagent-max-steps! db* chat-id)]
      (lifecycle/assert-chat-not-stopped! chat-ctx)
      ;; Check subagent max steps - if reached, finish without executing more tools
      (if max-steps-reached?
        (do
          (logger/info logger-tag "Subagent reached max steps, finishing" {:chat-id chat-id})
          (swap! db* assoc-in [:chats chat-id :max-steps-reached?] true)
          (when-not (string/blank? @received-msgs*)
            (add-to-history! {:role "assistant" :content [{:type :text :text @received-msgs*}]}))
          (lifecycle/finish-chat-prompt! :idle chat-ctx)
          nil)
        (do
          (when-not (string/blank? @received-msgs*)
            (add-to-history! {:role "assistant" :content [{:type :text :text @received-msgs*}]})
            (reset! received-msgs* ""))
          (let [parent-coordinator-id (when (some (partial spawn-agent-tool-call? all-tools) tool-calls)
                                        (parent-coordinator/create! chat-ctx))
                blocked-tool-call-info* (atom nil)]
            (try
              ;; preToolCall continue:false short-circuits the rest of this batch.
              (reduce (fn do-tool-call [_ {:keys [id full-name] :as tool-call}]
                      (let [approved?*                                                    (promise)
                            {:keys [origin name server parameters]
                             :as   resolved-tool}                                           (f.tools/resolve-tool full-name all-tools)
                            full-name                                                     (or (:full-name resolved-tool) full-name)
                            server-name                                                   (:name server)
                            spawn-agent?                                                  (and (= :native origin)
                                                                                               (= "eca" server-name)
                                                                                               (= "spawn_agent" name))
                            arguments                                                     (if parameters
                                                                                            (tools.util/omit-optional-empty-string-args parameters (:arguments tool-call))
                                                                                            (:arguments tool-call))
                            tool-call                                                     (assoc tool-call
                                                                                                 :full-name full-name
                                                                                                 :arguments (cond-> arguments
                                                                                                              spawn-agent? f.tools.agent/normalize-arguments))
                            decision-plan                                                 (decide-tool-call-action
                                                                                           tool-call all-tools @db* config agent chat-id
                                                                                           {:on-before-hook-action (partial lifecycle/notify-before-hook-action! chat-ctx)
                                                                                            :on-after-hook-action  (partial lifecycle/notify-after-hook-action! chat-ctx)
                                                                                            :trust                 (db/resolve-trust @db* chat-id)
                                                                                            :full-model            (:full-model chat-ctx)
                                                                                            :variant               (:variant chat-ctx)})
                            {:keys [decision tool-call-blocked-by-hook? reason stop-turn?
                                    stop-reason stop-hook-name]} decision-plan
                            ;; updatedInput is surfaced inside the real preToolCall
                            ;; hook finished block (see lifecycle/format-hook-output),
                            ;; so it respects visible:false / suppressOutput and the
                            ;; hook's real id/name. No synthetic event here.
                            arguments                                                     (cond-> (:arguments decision-plan)
                                                                                            spawn-agent? f.tools.agent/normalize-arguments)
                            _                                                             (swap! db* assoc-in [:chats chat-id :tool-calls id :arguments] arguments)
                            tool-call                                                     (assoc tool-call :arguments arguments)
                            ask?                                                          (= :ask decision)
                            details                                                       (f.tools/tool-call-details-before-invocation name arguments server @db* config chat-id ask? id)
                            summary                                                       (f.tools/tool-call-summary all-tools full-name arguments config @db*)]
                        (when-not (#{:stopping :cleanup} (:status (get-tool-call-state @db* chat-id id)))
                          (transition-tool-call! db* chat-ctx id :tool-run {:approved?*                approved?*
                                                                            :future-cleanup-complete?* (promise)
                                                                            :name                      name
                                                                            :server                    server-name
                                                                            :origin                    origin
                                                                            :arguments                 arguments
                                                                            :manual-approval           ask?
                                                                            :details                   details
                                                                            :summary                   summary}))
                        (when-not (#{:stopping :cleanup :rejected} (:status (get-tool-call-state @db* chat-id id)))
                          (case decision
                            :ask   (transition-tool-call! db* chat-ctx id :approval-ask {:progress-text "Waiting for tool call approval"})
                            :allow (transition-tool-call! db* chat-ctx id :approval-allow {:reason reason})
                            :deny  (transition-tool-call! db* chat-ctx id :approval-deny {:reason reason})
                            (logger/warn logger-tag "Unknown value of approval" {:approval decision :tool-call-id id})))
                        (if (and @approved?* (not tool-call-blocked-by-hook?))
                          (when-not (#{:stopping :cleanup} (:status (get-tool-call-state @db* chat-id id)))
                            (lifecycle/assert-chat-not-stopped! chat-ctx)
                            (let [delayed-future
                                  (delay
                                    (future
                                      (let [result               (f.tools/call-tool! full-name
                                                                                     arguments
                                                                                     chat-id
                                                                                     id
                                                                                     agent
                                                                                     db*
                                                                                     config
                                                                                     messenger
                                                                                     metrics
                                                                                     (partial get-tool-call-state @db* chat-id id)
                                                                                     (partial transition-tool-call! db* chat-ctx id)
                                                                                     {:trust (db/resolve-trust @db* chat-id)
                                                                                      :parent-coordinator-id parent-coordinator-id})
                                            details              (f.tools/tool-call-details-after-invocation name arguments details result
                                                                                                             {:db           @db*
                                                                                                              :config       config
                                                                                                              :chat-id      chat-id
                                                                                                              :tool-call-id id})
                                            {:keys [start-time]} (get-tool-call-state @db* chat-id id)
                                            total-time-ms        (- (System/currentTimeMillis) start-time)]
                                        (add-to-history! {:role    "tool_call"
                                                          :content (assoc tool-call
                                                                          :name name
                                                                          :details details
                                                                          :summary summary
                                                                          :origin origin
                                                                          :server server-name)})
                                        (add-to-history! {:role    "tool_call_output"
                                                          :content (assoc tool-call
                                                                          :name name
                                                                          :error (:error result)
                                                                          :output result
                                                                          :total-time-ms total-time-ms
                                                                          :details details
                                                                          :summary summary
                                                                          :origin origin
                                                                          :server server-name)})
                                        (let [state (get-tool-call-state @db* chat-id id) status (:status state)]
                                          (case status
                                            :executing (transition-tool-call! db*
                                                                              chat-ctx
                                                                              id
                                                                              :execution-end {:origin        origin
                                                                                              :name          name
                                                                                              :server        server-name
                                                                                              :arguments     arguments
                                                                                              :error         (:error result)
                                                                                              :outputs       (:contents result)
                                                                                              :total-time-ms total-time-ms
                                                                                              :progress-text "Generating"
                                                                                              :details       details
                                                                                              :summary       summary})
                                            :stopping  (transition-tool-call! db*
                                                                              chat-ctx
                                                                              id
                                                                              :stop-attempted {:origin        origin
                                                                                               :name          name
                                                                                               :server        server-name
                                                                                               :arguments     arguments
                                                                                               :error         (:error result)
                                                                                               :outputs       (:contents result)
                                                                                               :total-time-ms total-time-ms
                                                                                               :reason        :user-stop :details
                                                                                               details
                                                                                               :summary       summary})
                                            (logger/warn logger-tag "Unexpected value of :status in tool call" {:status status}))))))]
                              (transition-tool-call! db*
                                                     chat-ctx
                                                     id
                                                     :execution-start {:delayed-future delayed-future
                                                                       :origin         origin
                                                                       :name           name
                                                                       :server         server-name
                                                                       :arguments      arguments
                                                                       :start-time     (System/currentTimeMillis)
                                                                       :details        details
                                                                       :summary        summary
                                                                       :progress-text  (if (= name "spawn_agent")
                                                                                         "Waiting subagent"
                                                                                         "Calling tool")})))
                          (let [tool-call-state (get-tool-call-state @db* chat-id id)
                                {:keys [code text]} (:decision-reason tool-call-state)]
                            (add-to-history! {:role "tool_call" :content tool-call})
                            (add-to-history! {:role    "tool_call_output"
                                              :content (assoc tool-call :output {:error true :contents (rejected-tool-call-output-contents text)})})
                            (reset! blocked-tool-call-info* {:code code
                                                             :stop-turn? (when tool-call-blocked-by-hook? stop-turn?)
                                                             :stop-reason (when tool-call-blocked-by-hook? stop-reason)
                                                             :stop-hook-name (when tool-call-blocked-by-hook? stop-hook-name)})
                            (transition-tool-call! db* chat-ctx id :send-reject {:origin    origin
                                                                                 :name      name
                                                                                 :server    server-name
                                                                                 :arguments arguments
                                                                                 :reason    code
                                                                                 :details   details
                                                                                 :summary   summary})))
                        (when (and tool-call-blocked-by-hook? stop-turn?)
                          (reduced nil))))
                    nil
                    tool-calls)
            (lifecycle/assert-chat-not-stopped! chat-ctx)
            (doseq [[tool-call-id state] (get-active-tool-calls @db* chat-id)]
              (when-let [f (:future state)]
                (try (deref f)
                     (catch java.util.concurrent.CancellationException _
                       (when-let [p (:future-cleanup-complete?* state)]
                         (logger/debug logger-tag
                                       "Caught CancellationException.  Waiting for future to finish cleanup."
                                       {:tool-call-id tool-call-id :promise p})
                         (deref p)))
                     (catch Throwable t
                       (logger/debug logger-tag
                                     "Ignoring a Throwable while deref'ing a tool call future"
                                     {:tool-call-id tool-call-id
                                      :ex-data (ex-data t)
                                      :message (.getMessage ^Throwable t)
                                      :cause (.getCause ^Throwable t)}))
                     (finally (try (let [tool-call-state (get-tool-call-state @db* (:chat-id chat-ctx) tool-call-id)]
                                     (transition-tool-call!
                                      db*
                                      chat-ctx
                                      tool-call-id
                                      :cleanup-finished (merge {:name (:name tool-call-state)
                                                                :full-name (:full-name tool-call-state)}
                                                               (select-keys tool-call-state [:outputs :error :total-time-ms]))))
                                   (catch Throwable t
                                     (logger/debug logger-tag "Ignoring an exception while finishing tool call"
                                                   {:tool-call-id tool-call-id
                                                    :ex-data (ex-data t)
                                                    :message (.getMessage ^Throwable t)
                                                    :cause (.getCause ^Throwable t)})))))))
              (when parent-coordinator-id
                (when-let [summary (parent-coordinator/complete! db* parent-coordinator-id)]
                  (add-to-history! {:role "user"
                                    :content [{:type :text
                                               :text (str "<subagent-coordination-summary>\n"
                                                          summary
                                                          "\n</subagent-coordination-summary>")}]})))
              (f.tools.mcp/await-pending-tools-refresh @db* 5000)
            ;; Token can expire during long tool calls (e.g. spawn_agent),
            ;; so renew before any continuation branch.
            (lifecycle/maybe-renew-auth-token chat-ctx)
            (let [all-tools (f.tools/all-tools chat-id agent @db* config)
                  refreshed-provider-auth (get-in @db* [:auth (:provider chat-ctx)])
                  [_ refreshed-api-key] (llm-util/provider-api-key (:provider chat-ctx)
                                                                   refreshed-provider-auth
                                                                   config)
                  with-fresh-auth #(some-> % (assoc :fresh-api-key refreshed-api-key
                                                    :provider-auth refreshed-provider-auth))]
              (if-let [blocked-info @blocked-tool-call-info*]
                (let [reason-code
                      (if (map? blocked-info) (:code blocked-info) blocked-info)
                      stop-turn?
                      (when (map? blocked-info) (:stop-turn? blocked-info))
                      stop-reason
                      (when (map? blocked-info) (:stop-reason blocked-info))
                      stop-hook-name
                      (when (map? blocked-info) (:stop-hook-name blocked-info))]
                  (if (contains? #{:hook-rejected :hook-stopped} reason-code)
                    (if stop-turn?
                      ;; Turn stop: stopReason is user-only; tool_call_output stays neutral.
                      (do (lifecycle/send-turn-stopped-by-hook! chat-ctx stop-hook-name stop-reason)
                          (lifecycle/finish-chat-prompt-stopped! :idle chat-ctx)
                          nil)
                      (with-fresh-auth
                        {:tools all-tools
                         :new-messages (shared/messages-after-last-compact-marker
                                        (get-in @db* [:chats chat-id :messages]))}))
                    (if (get-in @db* [:chats chat-id :subagent])
                      ;; Subagent: user can't provide rejection input directly, so continue
                      ;; the LLM loop with a rejection message letting the subagent adapt
                      (do (add-to-history! {:role "user"
                                            :content [{:type :text
                                                       :text "I rejected one or more tool calls; they did not run and nothing was changed. Try a different approach to complete the task."}]})
                          (with-fresh-auth
                            {:tools all-tools
                             :new-messages (shared/messages-after-last-compact-marker
                                            (get-in @db* [:chats chat-id :messages]))}))
                      (do (lifecycle/send-content! chat-ctx :system {:type :text
                                                                     :text "Tell ECA what to do differently for the rejected tool(s)"})
                          (add-to-history! {:role "user"
                                            :content [{:type :text
                                                       :text "I rejected one or more tool calls with the following reason"}]})
                          (lifecycle/finish-chat-prompt! :idle chat-ctx)
                          nil))))
                (let [{:keys [stop-turn? stop-reason stop-hook-name]} (post-tool-call-stop-info
                                                                       (get-in @db* [:chats chat-id :tool-calls] {})
                                                                       (:prompt-id chat-ctx))]
                  (if stop-turn?
                    (do
                      (lifecycle/send-turn-stopped-by-hook! chat-ctx stop-hook-name stop-reason)
                      (lifecycle/finish-chat-prompt-stopped! :idle chat-ctx)
                      nil)
                    (with-fresh-auth
                      (if-let [continue-fn (:continue-fn chat-ctx)]
                        (continue-fn all-tools user-messages)
                        {:tools all-tools
                         :new-messages (shared/messages-after-last-compact-marker
                                        (get-in @db* [:chats chat-id :messages]))}))))))
              (finally
                (when parent-coordinator-id
                  (parent-coordinator/dispose! db* parent-coordinator-id))))))))))
